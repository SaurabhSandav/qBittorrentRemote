import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath Android.GRADLE_PLUGIN
        classpath Kotlin.GRADLE_PLUGIN
        classpath Navigation.SAFE_ARGS_GRADLE_PLUGIN

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

plugins {
    id "io.gitlab.arturbosch.detekt" version "1.0.0-RC12"
}

allprojects {
    repositories {
        google()
        mavenCentral()
        jcenter {
            content {

                // LeakCanary
                includeGroup "org.jetbrains.trove4j"

                // Detekt
                includeGroup "io.gitlab.arturbosch.detekt"
                includeGroup "com.beust"
            }
        }

        // TODO Temporary workaround (Unable to resolve dependencies)
        // https://github.com/afollestad/material-dialogs/issues/1575
        maven {
            url = uri("https://dl.bintray.com/drummer-aidan/maven/")
            content { includeGroup "com.afollestad.material-dialogs" }
        }
    }

    tasks.withType(KotlinCompile).all {
        kotlinOptions.freeCompilerArgs = ['-progressive', '-Xnew-inference']
    }

    apply from: "$rootDir/detekt.gradle"
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
